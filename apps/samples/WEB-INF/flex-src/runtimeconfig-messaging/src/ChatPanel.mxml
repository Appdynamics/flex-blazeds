<?xml version="1.0" encoding="utf-8"?>
<!--

  Licensed to the Apache Software Foundation (ASF) under one or more
  contributor license agreements.  See the NOTICE file distributed with
  this work for additional information regarding copyright ownership.
  The ASF licenses this file to You under the Apache License, Version 2.0
  (the "License"); you may not use this file except in compliance with
  the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.

-->
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
	creationComplete="initComp()">

	<mx:Script>
		<![CDATA[
			import mx.messaging.channels.StreamingAMFChannel;
			import mx.messaging.ChannelSet;
			import mx.messaging.channels.AMFChannel;
			import mx.messaging.events.MessageEvent;
			import mx.messaging.messages.AsyncMessage;
			import mx.messaging.messages.IMessage;


			private function initComp():void
			{
				var myStreamingAMF:AMFChannel = new StreamingAMFChannel("my-streaming-amf", "../messagebroker/streamingamf");
				var myPollingAMF:AMFChannel = new AMFChannel("my-polling-amf", "../messagebroker/amfpolling");
				myPollingAMF.pollingEnabled = true;
				myPollingAMF.pollingInterval = 2000;
				var channelSet:ChannelSet = new ChannelSet();
				channelSet.addChannel(myStreamingAMF);
				channelSet.addChannel(myPollingAMF);
				consumer.channelSet = channelSet;				
				producer.channelSet = channelSet;
			}

			public function set room(name:String):void
			{
				if (!name) return;
				
				if (consumer && consumer.subscribed) 
				{
					log.text += "Leaving room " + consumer.destination + "\n";	
					consumer.unsubscribe();
				}
				consumer.destination = name;
				producer.destination = name;
				consumer.subscribe();
				log.text += "Entering room " + name + "\n";	
			}
			
			private function send():void
			{
				var message:IMessage = new AsyncMessage();
				message.body = msg.text;
				producer.send(message);
				msg.text = "";
			}
							
			private function messageHandler(event:MessageEvent):void
			{
				log.text += event.message.body + "\n";	
			}
			
		]]>
	</mx:Script>

	<mx:Producer id="producer"/>
	<mx:Consumer id="consumer" message="messageHandler(event)"/>

	<mx:TextArea id="log" width="100%" height="100%"/>
	
	<mx:ControlBar>
		<mx:TextInput id="msg" width="100%" enter="send()" enabled="{consumer.subscribed}"/>
		<mx:Button label="Send" click="send()" enabled="{consumer.subscribed}"/>
	</mx:ControlBar>
	
</mx:Panel>
